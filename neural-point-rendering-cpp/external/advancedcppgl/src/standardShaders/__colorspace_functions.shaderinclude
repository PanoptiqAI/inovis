//different color spaces
//mostly from: https://github.com/tobspr/GLSL-Color-Spaces/blob/master/ColorSpaces.inc.glsl


// RGB to YCbCr, ranges [0, 1]
vec3 rgb_to_ycbcr(vec3 rgb) {
	float y = 0.299 * rgb.r + 0.587 * rgb.g + 0.114 * rgb.b;
	float cb = (rgb.b - y) * 0.565;
	float cr = (rgb.r - y) * 0.713;

	return vec3(y, cb, cr);
}

// YCbCr to RGB
vec3 ycbcr_to_rgb(vec3 yuv) {
	return vec3(
		yuv.x + 1.403 * yuv.z,
		yuv.x - 0.344 * yuv.y - 0.714 * yuv.z,
		yuv.x + 1.770 * yuv.y
	);
}

float getYLuminance(vec3 rgb) {
	return 0.299 * rgb.r + 0.587 * rgb.g + 0.114 * rgb.b;
}

// srgb to linear conversion
float toLinear(float val) {
	const float f1 = 1.f / 12.92f;
	const float f2 = 1.f / 1.055f;
	if (val <= 0.04045)
		return val * f1;
	else
		return pow((val + 0.055) * f2, 2.4f);
}

vec3 toLinear3(vec3 srgb) {
	return vec3(toLinear(srgb.x), toLinear(srgb.y), toLinear(srgb.z));
}

